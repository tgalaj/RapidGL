#version 460

layout(binding = 0)			 uniform sampler2D u_input_texture;
layout(rgba32f, binding = 0) uniform image2D   u_output_image;

layout(binding = 1)			 uniform sampler2D u_dirt_texture;

uniform float u_sample_scale;
uniform vec2  u_texel_size;
uniform int   u_mip_level;
uniform float u_bloom_intensity;
uniform float u_dirt_intensity;

#define GROUP_SIZE         8
#define GROUP_THREAD_COUNT (GROUP_SIZE * GROUP_SIZE)
#define FILTER_SIZE        3
#define FILTER_RADIUS      (FILTER_SIZE / 2)
#define TILE_SIZE          (GROUP_SIZE + 2 * FILTER_RADIUS)
#define TILE_PIXEL_COUNT   (TILE_SIZE * TILE_SIZE)

shared vec4 sm_texels[TILE_PIXEL_COUNT];

layout(local_size_x = GROUP_SIZE, local_size_y = GROUP_SIZE) in;
void main()
{
	ivec2 pixel_coords = ivec2(gl_GlobalInvocationID.xy);
    ivec2 base_index   = ivec2(gl_WorkGroupID) * GROUP_SIZE - FILTER_RADIUS;

    // The first (TILE_PIXEL_COUNT - GROUP_THREAD_COUNT) threads load at most 2 texel values
    for (int i = int(gl_LocalInvocationIndex); i < TILE_PIXEL_COUNT; i += GROUP_THREAD_COUNT)
    {
        vec2 uv        = (vec2(base_index) + 0.5) * u_texel_size;
        vec2 uv_offset = vec2(i % TILE_SIZE, i / TILE_SIZE) * u_texel_size;
        
        vec4 color = textureLod(u_input_texture, uv + uv_offset * u_sample_scale, u_mip_level);
        sm_texels[i] = color;
    }

    memoryBarrierShared();
    barrier();

    // center texel
    uint sm_idx = (gl_LocalInvocationID.x + FILTER_RADIUS) + (gl_LocalInvocationID.y + FILTER_RADIUS) * TILE_SIZE;

     // Based on [Jimenez14] http://goo.gl/eomGso
     vec4 s;
     s =  sm_texels[sm_idx - TILE_SIZE - 1];
     s += sm_texels[sm_idx - TILE_SIZE    ] * 2.0;
     s += sm_texels[sm_idx - TILE_SIZE + 1];
	 
     s += sm_texels[sm_idx - 1] * 2.0;
     s += sm_texels[sm_idx    ] * 4.0;
     s += sm_texels[sm_idx + 1] * 2.0;
	 
     s += sm_texels[sm_idx + TILE_SIZE - 1];
     s += sm_texels[sm_idx + TILE_SIZE    ] * 2.0;
     s += sm_texels[sm_idx + TILE_SIZE + 1];
	 vec4 bloom = s * (1.0 / 16.0);

	vec4 out_pixel = imageLoad(u_output_image, pixel_coords);
	     out_pixel += bloom * u_bloom_intensity;

    if (u_mip_level == 1)
    {
        vec2  uv  = (vec2(pixel_coords) + vec2(0.5, 0.5)) * u_texel_size;
        out_pixel += texture(u_dirt_texture, uv) * u_dirt_intensity * bloom * u_bloom_intensity;
    }

	imageStore(u_output_image, pixel_coords, out_pixel);
}